import "example/stdio.fk"

enum {
    OP_PUSH = 1
    OP_PLUS
    OP_PRINT
}

struct Inst { type op }

struct VM {
    stack = []
    ip = 0

    def push(s, v) {
        s.stack = append(s.stack, v)
    }

    def pop(s) {
        sz = len(s.stack)
        rs = s.stack[sz-1]
        s.stack = remove(s.stack, sz-1)
        return rs
    }
}

def execute(prog) {
    vm = VM()

    while (vm.ip < len(prog)) : (vm.ip += 1) {
        inst = prog[vm.ip]
        case (inst.type) {
            OP_PUSH  => vm.push(inst.op)
            OP_PLUS  => vm.push(vm.pop() + vm.pop())
            OP_PRINT => println(vm.pop())
            else     => println("unknown instruction")
        }
    }
}

prog = [
    Inst(OP_PUSH, 33),
    Inst(OP_PUSH, 36),
    Inst(OP_PLUS),
    Inst(OP_PRINT),
]

execute(prog)
